cmake_minimum_required(VERSION 3.14)

set(LIB_PROJECT_NAME remez)
set(LIB_TARGET_NAME ${LIB_PROJECT_NAME})
set(PROJECT_MACRO_PREFIX REMEZ)

project(${LIB_PROJECT_NAME})

set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)

set(
    LIB_SOURCES
    src/remez.c
)

# Common

project(${LIB_PROJECT_NAME})

set(LIB_DEV_PUBLIC_HEADER_DIR "${PROJECT_SOURCE_DIR}/include")
file(GLOB_RECURSE HEADERS "${LIB_DEV_PUBLIC_HEADER_DIR}/${LIB_TARGET_NAME}" *.h)

add_library(
    ${LIB_TARGET_NAME}
    ${HEADERS}
    ${LIB_SOURCES}
)

set(EXPORT_FILE "${LIB_DEV_PUBLIC_HEADER_DIR}/${LIB_PROJECT_NAME}/${LIB_PROJECT_NAME}_export.h")
set(CMAKE_CXX_VISIBILITY_PRESET hidden)
set(CMAKE_VISIBILITY_INLINES_HIDDEN 1)

include(GenerateExportHeader)
generate_export_header(
    ${LIB_TARGET_NAME}
    BASE_NAME ${PROJECT_MACRO_PREFIX}
    EXPORT_MACRO_NAME ${PROJECT_MACRO_PREFIX}_PUBLIC
    NO_EXPORT_MACRO_NAME ${PROJECT_MACRO_PREFIX}_PRIVATE
    STATIC_DEFINE ${PROJECT_MACRO_PREFIX}_STATIC_BUILD
    EXPORT_FILE_NAME ${EXPORT_FILE}
)

target_include_directories(${LIB_TARGET_NAME} INTERFACE "$<INSTALL_INTERFACE:include>")
target_include_directories(${LIB_TARGET_NAME} BEFORE PRIVATE "${PROJECT_SOURCE_DIR}/include")

if (WIN32)
    install(
        TARGETS ${LIB_TARGET_NAME}
        EXPORT ${LIB_TARGET_NAME}
        RUNTIME DESTINATION bin
        LIBRARY DESTINATION bin
        ARCHIVE DESTINATION lib
    )

    install(FILES $<TARGET_PDB_FILE:${LIB_TARGET_NAME}> DESTINATION bin OPTIONAL)
else ()
    install(
        TARGETS ${LIB_TARGET_NAME}
        EXPORT ${LIB_TARGET_NAME}
        LIBRARY DESTINATION lib
        ARCHIVE DESTINATION lib
    )
endif ()

install(DIRECTORY "${LIB_DEV_PUBLIC_HEADER_DIR}/${LIB_TARGET_NAME}" DESTINATION include/)
install(EXPORT "${LIB_TARGET_NAME}" DESTINATION "lib/cmake/${LIB_TARGET_NAME}" FILE "${LIB_TARGET_NAME}-config.cmake")

option(USE_TESTS "Enable unit testing" ON)
if (USE_TESTS AND IS_DIRECTORY "${PROJECT_SOURCE_DIR}/tests")
    message("Tests enabled.")
    enable_testing()
    add_subdirectory(tests)
endif ()

# End of common section
